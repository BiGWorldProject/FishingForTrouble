IF
  Dead("ysakil")
THEN
  RESPONSE #100
    SetGlobal("ysAkilIsDead","GLOBAL",1)
    SetGlobal("ysKillAkil","GLOBAL",0)
END

IF
  Dead("yslevon")
THEN
  RESPONSE #100
    DialogInterrupt(TRUE)
END

IF
  Global("ysTalkedToAkil","GLOBAL",100)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
    Continue()
END

//Find a target
IF
  Allegiance(Myself,ENEMY)
	See([GOODCUTOFF])
THEN
	RESPONSE #100
		Continue()
END

//Fighting with Protagonist against Levon and guards
IF
  See([EVILCUTOFF])
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    DialogInterrupt(FALSE)
    Continue()
END

// * Potion of Extra Healing aka Cure Serious Wounds
IF
	HPPercentLT(Myself,25)
	GlobalLT("ys_ExHeal","LOCALS",2)
THEN
	RESPONSE #100
		IncrementGlobal("ys_ExHeal","LOCALS",1)
		ReallyForceSpell(Myself,CLERIC_CURE_SERIOUS_WOUNDS)
//			DisplayStringHead(Myself,18893)							// say "Potion of Extra Healing"
END

//If I'm being attacked then select my attacker as a new target
IF
  Allegiance(Myself,ENEMY)
	OR(3)
		AttackedBy([ANYONE],DEFAULT)
		AttackedBy([ANYONE],MELEE)
		AttackedBy([ANYONE],RANGED)
	See(LastAttackerOf(Myself))
THEN
	RESPONSE #100
		Continue()
END

//Select weapon
IF
  Allegiance(Myself,ENEMY)
	!Range(LastSeenBy(Myself),5)
THEN
	RESPONSE #100
		EquipRanged()
		Attack(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
	Range(LastSeenBy(Myself),5)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		Attack(LastSeenBy(Myself))
END
