IF
  OR(2)
	 Global("ysblgrd","GLOBAL",1)
	 Global("ysblgrd","GLOBAL",3)
  See([PC])
THEN
	RESPONSE #100
		Dialogue(Protagonist)
END

IF
  OR(3)
    Global("ysblgrd","GLOBAL",2)
    GlobalTimerExpired("ys_PrimeBLGuards","GLOBAL")
    Global("ys_RedCompanyFight","GLOBAL",1)
  !Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Enemy()
END

// Fighting script - move to the fight
// Break off when any party member is seen
IF
  Allegiance(Myself,ENEMY)
	!See([GOODCUTOFF])
THEN
	RESPONSE #15
	  MoveToObject(Player1)
		Continue()
//Left fire
	RESPONSE #20
	  MoveToPoint([1737.917])
		Continue()
//Right fire
	RESPONSE #15
	  MoveToPoint([1985.944])
		Continue()
//Near cave mouth
	RESPONSE #25
	  MoveToPoint([2425.1089])
		Continue()
//South side of caravan
	RESPONSE #25
	  MoveToPoint([1339.1246])
		Continue()
END

//Find an initial target
IF
  Allegiance(Myself,ENEMY)
	See([GOODCUTOFF])
THEN
	RESPONSE #100
		Continue()
END

//Potion of Healing aka Cure Moderate Wounds
IF
	HPPercentLT(Myself,25)
	GlobalLT("ys_ExHeal","LOCALS",2)
THEN
	RESPONSE #100
		IncrementGlobal("ys_ExHeal","LOCALS",1)
		ReallyForceSpell(Myself,CLERIC_CURE_MEDIUM_WOUNDS)
//			DisplayStringHead(Myself,18893)							// say "Potion of Extra Healing"
END

//If I'm being attacked then select my attacker as a new target
IF
  Allegiance(Myself,ENEMY)
	OR(3)
		AttackedBy([ANYONE],DEFAULT)
		AttackedBy([ANYONE],MELEE)
		AttackedBy([ANYONE],RANGED)
	See(LastAttackerOf(Myself))
THEN
	RESPONSE #100
		Continue()
END

//Select weapon
IF
  Allegiance(Myself,ENEMY)
	!Range(LastSeenBy(Myself),5)
THEN
	RESPONSE #100
		EquipRanged()
		Attack(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
	Range(LastSeenBy(Myself),5)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		Attack(LastSeenBy(Myself))
END
