BACKUP ~fishingfortrouble/backup~
AUTHOR ~www.shsforums.net/forum/550-fishing-for-trouble/~
VERSION ~v3.1.1 pre-release~
//MODDER setup_tra NONE
//launch the ReadMe file immediately
README ~fishingfortrouble/readme.html~

ALWAYS
	ACTION_IF (GAME_IS ~eet~) AND (FILE_CONTAINS ~override/EET.flag~ ~EET_JOURNAL~) BEGIN
		OUTER_SET bg2_chapter = 14
	END ELSE BEGIN
		OUTER_SET bg2_chapter = 0
	END
	OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
		OUTER_SET bg2_chapter = bg2_chapter + 1
		OUTER_SPRINT name_source ~bg2_chapter_%i%~
		OUTER_SET EVAL ~%name_source%~ = bg2_chapter
	END
	ACTION_IF GAME_IS ~eet~ BEGIN
		OUTER_SET wmp_xCoord_SoA = 0
		OUTER_SET wmp_yCoord_SoA = 2283
	END ELSE BEGIN
		OUTER_SET wmp_xCoord_SoA = 0
		OUTER_SET wmp_yCoord_SoA = 0
	END
END

LANGUAGE ~English (Yovaneth)~ ~english~ ~fishingfortrouble/language/english/english.tra~
LANGUAGE ~Russian (subzero400)~ ~russian~ ~fishingfortrouble/language/russian/russian.tra~

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//Core component: Fishing for Trouble by Yovaneth
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
BEGIN @20000
REQUIRE_PREDICATE GAME_INCLUDES ~TOB~ ~This mod requires Baldur's Gate II: Throne of Bhaal or Enhanced Edition~

//##################################################
// Add new areas to the worldmap
//##################################################
INCLUDE ~fishingfortrouble/wmp/worldmap.tph~

//##################################################
// Apply IDS and EXE patches if necessary (no TobEx)
//##################################################
INCLUDE ~fishingfortrouble/lib/patches.tpa~

//##################################################
// Copy new material
//##################################################
//Spells
COPY ~fishingfortrouble/spells/yselmin.spl~ ~override~
	SAY NAME1 @149

COPY ~fishingfortrouble/spells/ysghoul.spl~ ~override~

//##################################################
//Cutscenes
//##################################################
//Cerendor Hold
COMPILE ~fishingfortrouble/cutscenes/yscutgte.baf~
COMPILE ~fishingfortrouble/cutscenes/yscut01.baf~
COMPILE ~fishingfortrouble/cutscenes/yscut02.baf~
COMPILE ~fishingfortrouble/cutscenes/yscut03.baf~

//##################################################
//Bams
//##################################################
COPY ~fishingfortrouble/bams~ ~override~

//##################################################
//Music
//##################################################
COPY ~fishingfortrouble/audio/ysblank.mus~ ~music~
COPY_EXISTING ~songlist.2da~ ~override~
	SET_2DA_ENTRY 0 2 3 ~YSBLANK.MUS~ // "no_music" resource

//##################################################
//Sounds
//##################################################
	COPY ~fishingfortrouble/sounds~ ~override~

//##################################################
//Ghoul portrait
//##################################################
	COPY ~fishingfortrouble/bmp~ ~override~

//##################################################
//2da
//##################################################
COPY_EXISTING ~itemdial.2da~ ~override~
	BUT_ONLY_IF_IT_CHANGES

APPEND ~itemdial.2da~ ~YSPRPT		4978	YSPRPT~
	UNLESS ~YSPRPT[ %TAB%]4978[ %TAB%]YSPRPT~
APPEND ~itemdial.2da~ ~YSLABRD		4978	YSLABRD~
	UNLESS ~YSLABRD[ %TAB%]4978[ %TAB%]YSLABRD~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~racetext.2da~ BEGIN
	COPY ~fishingfortrouble/2da/racetext.2da~ ~override~
END ELSE BEGIN
	APPEND ~racetext.2da~ ~108	15946~ UNLESS ~^108~
END

//##################################################
//Items
//##################################################
INCLUDE ~fishingfortrouble/lib/items.tpa~

//##################################################
//Dialog files
//##################################################
INCLUDE ~fishingfortrouble/lib/dialog.tpa~

//##################################################
//Areas
//##################################################
INCLUDE ~fishingfortrouble/lib/areas.tpa~

//##################################################
//Actor scripts
//##################################################
INCLUDE ~fishingfortrouble/lib/actor_scripts.tpa~

//##################################################
//Area scripts
//##################################################
INCLUDE ~fishingfortrouble/lib/area_scripts.tpa~

//##################################################
//Other area scripts
//##################################################
EXTEND_TOP ~baldur.bcs~ ~fishingfortrouble/area_scripts/ysbaldur.baf~

//##################################################
//Stores
//##################################################
INCLUDE ~fishingfortrouble/lib/stores.tpa~

//##################################################
//Uncompressed Tilesets
//##################################################
COPY ~fishingfortrouble/tis~ ~override~

//##################################################
//SConrad's Worldmap patching - do this last so we can patch the new areas too
//##################################################
INCLUDE ~fishingfortrouble/lib/patch_worldmap.tpa~

//##################################################
// .ogg and .tiz conversion
//##################################################
LAF HANDLE_AUDIO END // audio folder
LAF HANDLE_TILESETS END // tiz folder

// End of core component

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//Optional component - install major character portraits
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
BEGIN @20001
REQUIRE_COMPONENT ~setup-fishingfortrouble.tp2~ 0 ~This component requires the main component.~

COPY ~fishingfortrouble/portraits~ ~override~

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//Optional component - add Fishing for Trouble to BP-BGT-Worldmap
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
BEGIN @20002
REQUIRE_COMPONENT ~setup-fishingfortrouble.tp2~ 0 ~This component requires the main component.~

ACTION_IF FILE_EXISTS ~Worldmap/map_mods_areas.tbl~ THEN BEGIN
	COPY ~Worldmap/map_mods_areas.tbl~						~Worldmap~
		APPEND_FILE ~fishingfortrouble/Worldmap/areas.tbl~
	COPY ~Worldmap/map_mods_links.tbl~						~Worldmap~
		APPEND_FILE ~fishingfortrouble/Worldmap/links.tbl~
	COPY - ~fishingfortrouble/Worldmap/worldmap.tra~		~tmp_worldmap.tra~
	COPY ~Worldmap/map_mods_trans.tra~						~Worldmap~
		APPEND_FILE ~tmp_worldmap.tra~
END ELSE BEGIN
	MKDIR ~Worldmap~
	COPY ~fishingfortrouble/Worldmap/areas.tbl~				~Worldmap/map_mods_areas.tbl~
	COPY ~fishingfortrouble/Worldmap/links.tbl~				~Worldmap/map_mods_links.tbl~
	COPY ~fishingfortrouble/Worldmap/worldmap.tra~			~Worldmap/map_mods_trans.tra~
END
